FUNCTION_BLOCK tipper	// Block definition 

VAR_INPUT				// Define input variables
	LuzExterior : REAL;
	Temperatura : REAL;
END_VAR

VAR_OUTPUT				// Define output variable
	PotenciaFoco : REAL;
END_VAR

FUZZIFY LuzExterior			// Fuzzify input variable 'LuzExterior': {'Baja', 'LuzAmbiente' , 'Alta'}
	TERM Baja := (0, 1) (10,1) (30, 0) ; 
	TERM LuzAmbiente := (10, 0) (50,1) (90,0);
	TERM Alta := (70, 0) (83,1) (100, 1);
END_FUZZIFY

FUZZIFY Temperatura			// Fuzzify input variable 'Temperatura': { 'Frio', 'TemperaturaAmbiente', 'Caliente'}
	TERM Frio := (0, 1) (9, 1) (20,0) ;
	TERM TemperaturaAmbiente := (15,0) (28,1) (40,0);
	TERM Caliente := (35,0) (50,1) (100,1);
END_FUZZIFY

DEFUZZIFY PotenciaFoco			// Defzzzify output variable 'PotenciaFoco' : {'Apagado', 'MedioPrendido', 'Prendido' }
	TERM Apagado := (0,1) (75,0);
	TERM MedioPrendido := (50,0) (125,1) (200,0);
	TERM Prendido := (200,0) (255,1);
	METHOD : COG;		// Use 'Center Of Gravity' defuzzification method
	DEFAULT := 0;		// Default value is 0 (if no rule activates defuzzifier)
END_DEFUZZIFY

// Inference rules
RULEBLOCK No1
	AND : MIN;			// Use 'min' for 'and' (also implicit use 'max' for 'or' to fulfill DeMorgan's Law)
	ACT : MIN;			// Use 'min' activation method
	ACCU : MAX;			// Use 'max' accumulation method

	RULE 1 : IF LuzExterior IS Baja AND Temperatura IS Frio THEN PotenciaFoco IS Prendido;
        RULE 2 : IF LuzExterior IS Baja AND Temperatura IS TemperaturaAmbiente THEN PotenciaFoco IS MedioPrendido;
        RULE 3 : IF LuzExterior IS Baja AND Temperatura IS Caliente THEN PotenciaFoco IS MedioPrendido;
        RULE 4 : IF LuzExterior IS LuzAmbiente AND Temperatura IS Frio THEN PotenciaFoco IS MedioPrendido;
        RULE 5 : IF LuzExterior IS LuzAmbiente AND Temperatura IS TemperaturaAmbiente THEN PotenciaFoco IS MedioPrendido;
        RULE 6 : IF LuzExterior IS LuzAmbiente AND Temperatura IS Caliente THEN PotenciaFoco IS Apagado;
        RULE 7 : IF LuzExterior IS Alta AND Temperatura IS Frio THEN PotenciaFoco IS MedioPrendido;
        RULE 8 : IF LuzExterior IS Alta AND Temperatura IS TemperaturaAmbiente THEN PotenciaFoco IS Apagado;
        RULE 9 : IF LuzExterior IS Alta AND Temperatura IS Caliente THEN PotenciaFoco IS Apagado;
END_RULEBLOCK

END_FUNCTION_BLOCK
