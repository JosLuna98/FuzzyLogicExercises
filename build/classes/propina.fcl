FUNCTION_BLOCK tipper	// Block definition 

VAR_INPUT				// Define input variables		
	Servicio : REAL;
	Comida : REAL;
END_VAR

VAR_OUTPUT				// Define input variables
	Propina : REAL;
END_VAR

FUZZIFY Servicio                         // Fuzzify input variable 'Servicio': {'Pobre', 'Bueno' , 'Excelente'}
	TERM Pobre := (0, 1) (4, 0); 
	TERM Bueno := (1, 0) (4,1) (6,1) (9,0);
	TERM Excelente := (6, 0) (9, 1);
END_FUZZIFY

FUZZIFY Comida                            // Fuzzify input variable 'Comida': { 'Rancia', 'Buena', 'Deliciosa' }
	TERM Rancia := (0, 1) (1, 1) (3,0);
        TERM Buena := (3, 0) (4, 1) (6, 1) (7,0);
	TERM Deliciosa := (7,0) (9,1);
END_FUZZIFY

DEFUZZIFY Propina                           // Defzzzify output variable 'Propina' : {'Barata', 'Promedio', 'Generosa' }
	TERM Barata := (0,0) (5,1) (10,0);
	TERM Promedio := (10,0) (15,1) (20,0);
	TERM Generosa := (20,0) (25,1) (30,0);
	METHOD : COG;		// Use 'Center Of Gravity' defuzzification method
	DEFAULT := 0;		// Default value is 0 (if no rule activates defuzzifier)
END_DEFUZZIFY

// Inference rules
RULEBLOCK No1
	AND : MIN;	// Use 'min' for 'and'
	ACT : MIN;	// Use 'min' activation method
	ACCU : MAX;	// Use 'max' accumulation method

	RULE 1 : IF Servicio IS Pobre OR Comida IS Rancia THEN Propina IS Barata;
	RULE 2 : IF Servicio IS Bueno OR Comida IS Buena THEN Propina IS Promedio; 
	RULE 3 : IF Servicio IS Excelente AND Comida IS Deliciosa THEN Propina IS Generosa;
END_RULEBLOCK

END_FUNCTION_BLOCK
